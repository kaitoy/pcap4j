/*_##########################################################################
  _##
  _##  Copyright (C) 2016 Pcap4J.org
  _##
  _##########################################################################
*/

import org.gradle.internal.os.OperatingSystem

configurations {
  captureScript
}

dependencies {
  captureScript project(':pcap4j-core')
  captureScript project(':pcap4j-packetfactory-static')
  captureScript project(':pcap4j-sample')
}

def scriptDir = "${buildDir}/script"

task genScript(
  type: Copy
) {
  from configurations.captureScript
  into scriptDir
  doLast {
    def jars = new File(scriptDir).listFiles()
            .findAll { it.name.endsWith '.jar' }
            .collect { it.name }
    if (OperatingSystem.current().isWindows()) {
      new File("${scriptDir}/capture.bat").withWriter { out ->
        out.println "@echo off"
        out.println "\"%JAVA_HOME%\\bin\\java\" -cp ${String.join ';', jars.toArray(new String[0])} org.pcap4j.sample.Loop"
      }
    }
    else {
      new File("${scriptDir}/capture.sh").withWriter { out ->
        out.println "#!/bin/sh"
        out.println "\$JAVA_HOME/bin/java -cp ${String.join ':', jars.toArray(new String[0])} org.pcap4j.sample.Loop"
      }
    }
  }
}

